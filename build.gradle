plugins {
    id 'maven-publish'
}

allprojects {
    ext {
        artifactName = 'protobufs'
    }
    version = '1.0.0'
    group = 'org.opensearch'
    var isSnapshot = System.getProperty("build.snapshot", "true")
    if (isSnapshot.toBoolean() && !version.endsWith("SNAPSHOT")) {
        version = version + "-SNAPSHOT"
    }
}

repositories {
    mavenCentral()
}

publishing {
    publications {
        create(MavenPublication) {
            groupId = group
            artifactId = artifactName
            artifact "./generated/maven/publish/${artifactName}-${version}.jar"

        }
    }

    repositories {
        maven{
            name = 'Snapshots'
            url = uri("https://aws.oss.sonatype.org/content/repositories/snapshots/")

            credentials {
                username = System.getenv("SONATYPE_USERNAME")
                password = System.getenv("SONATYPE_PASSWORD")
            }
        }
        maven {
            name = "localRepo"
            url = "${rootProject.buildDir}/repository"
        }
    }
}

// Override maven publish generated pom with the one generated by ./tools/java/package_proto_jar.sh
tasks.named('generatePomFileForCreatePublication') {
    doLast {
        def generatedPom = file("$rootProject.buildDir/publications/create/pom-default.xml")
        generatedPom.delete()
        generatedPom << file("./generated/maven/publish/${artifactName}-${version}.pom").text
    }
}

task clean(type: Delete) {
    delete "${rootProject.buildDir}"
}

task cleanGenerated(type: Delete) {
    delete "${rootProject.projectDir}/generated"
}
